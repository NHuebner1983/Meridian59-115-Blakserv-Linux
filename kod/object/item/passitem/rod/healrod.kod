// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
HealWand is Rod

constants:

   include blakston.khd

resources:

   include healrod.lkod

   healWand_name_rsc = "rod of healing"
   healWand_icon_rsc = healwand.bgf
   healWand_desc_rsc = \
      "This rod is almost comically decorated by obviously fake gems. "
      "Still, the rod hums nearly inaudibly, and when you put your hand on "
      "it, it is warm to the touch."
   healWand_success_rsc = \
      "The rod pulses once in your hand, restoring ~B~g%d~n health."

classvars:

   vrName = healWand_name_rsc
   vrIcon = healWand_icon_rsc
   vrGoodDesc = healWand_desc_rsc
   vrSuccess = healWand_success_rsc

   viBulk = 20
   viWeight = 25
   viValue_average = 200
   viInventory_group = 2

properties:

   piHeal_min = 4
   piHeal_max = 8

messages:

   CastSpell()
   "Heals the owner."
   {
      local amount, amount_healed;
      
      amount = Random(piHeal_min*100, piHeal_max*100);
      
      amount_healed = Send(poOwner,@GainHealthNormal,#amount=amount,
                           #precision=TRUE);
      Send(poOwner,@MsgSendUser,#message_rsc=vrSuccess,
                           #parm1=amount_healed/100);
      Send(Send(poOwner,@GetOwner),@SomethingWaveRoom,#what=poOwner,
                           #wave_rsc=vrSuccessSound);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
