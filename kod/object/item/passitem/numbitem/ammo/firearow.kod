// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
//an Arrow is a NumberItem used by a bow--it needs properties and procedures
//for ShowSomething() and GetDamage()
//
FireArrow is Ammo

constants:

   include blakston.khd

resources:

   include firearow.lkod

   fire_arrow_name_rsc = "fire arrows"
   fire_arrow_icon_rsc = arrowfir.bgf
   fire_arrow_desc_rsc = \
      "These arrows do excellent damage, burning "
      "brightly, scorching your enemy."

   fire_arrow_inflames = "The fire arrowhead inflames!"

classvars:

   vrName = fire_arrow_name_rsc
   vrIcon = fire_arrow_icon_rsc
   vrDesc = fire_arrow_desc_rsc

   viAmmo_type = AMMO_ARROW
   viValue_average = 20

   viGround_group = 4
   viInventory_group = 3
   viBroken_group = 2

   viBulk = 4
   viWeight = 4

   viMinDamage = 2
   viMaxDamage = 6

   viMinFireDamage = 3
   viMaxFireDamage = 8

properties:

   piNumber = 10
   piAttack_spell = ATCK_SPELL_FIRE

messages:

   GetDamage(who=$,target=$)
   {
      if target <> $ AND Random(1,100) <= 10
      {
         Send(who,@MsgSendUser,#message_rsc=fire_arrow_inflames);
         if IsClass(target,&Player)
         {
            Send(target,@MsgSendUser,#message_rsc=fire_arrow_inflames);
         }

         return Random(viMinFireDamage,viMaxFireDamage);
      }

      propagate;
   }

   GetMinSpecialDamage()
   {
      return viMinFireDamage;
   }

   GetMaxSpecialDamage()
   {
      return viMaxFireDamage;
   }

   SendProjectileAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,1);

      return;
   }

   GetProjectileSpeed()
   {
      return 7;
   }

   GetProjectileIcon()
   {
      return vrIcon;
   }

end
////////////////////////////////////////////////////////////////////////////////
