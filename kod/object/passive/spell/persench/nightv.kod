// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
NightVision is PersonalEnchantment

constants:

   include blakston.khd

resources:

   include nightv.lkod

   nightvision_name_rsc = "night vision"
   nightvision_icon_rsc = nitvsico.bgf
   nightvision_desc_rsc = \
      "Magically enhances the vision of the target.  "
      "Requires three elderberries to cast."
   nightvision_enchantment_rsc = \
      "\n\nYour current %s enchantment adds %i visual light."
   nightvision_already_enchanted_rsc = "You already have night vision."

   nightvision_on_rsc = "Your eyes burn with magical energy."
   nightvision_off_rsc = "Your vision returns to normal."
   nightvision_success_rsc = "%s%s's eyes burn with magical energy."

classvars:

   vrName = nightvision_name_rsc
   vrIcon = nightvision_icon_rsc
   vrDesc = nightvision_desc_rsc

   vrAlreadyEnchanted = nightvision_already_enchanted_rsc
   vrEnchantment_On = nightvision_On_rsc
   vrEnchantment_Off = nightvision_Off_rsc
   vrSuccess = nightvision_Success_rsc

   viSpell_num = SID_NIGHT_VISION
   viSchool = SS_KRAANAN
   viSpell_level = 3
   viMana = 6

   viChance_To_Increase = 35
   viMeditate_ratio = 10

   viFlash = FLASH_GOOD_SELF

properties:

   // The effectiveness of purge on the target spell as a percentage.
   // 100 will cause purge to remove its entire spellpower, 0 will
   // prevent purge from removing this spell.
   viPurgeFactor = 80

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&ElderBerry,3],plReagents);

      return;
   }

   GetStateValue(who=$,iSpellPower=0,target=$)
   {
      local iState;

      iState = iSpellPower + 50;
      Send(target,@GainLight,#amount=iState);

      return iState;
   }

   GetDuration(iSpellpower=0)
   {
      local iDuration;

      iDuration = 1500 + 10 * iSpellPower;
      iDuration = iDuration * 1000;

      return iDuration;
   }


   EndEnchantment(who=$,report=TRUE,state=0)
   {
      Send(who,@LoseLight,#amount=state);

      propagate;
   }

   GetPotionClass()
   {
      return &NightVisionPotion;
   }

   EffectDesc(who=$)
   {
      AddPacket(4,nightvision_enchantment_rsc, 4,Send(self,@GetName),
                4,Send(who,@GetEnchantedState,#what=self));

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
