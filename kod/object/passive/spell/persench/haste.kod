// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Haste is PersonalEnchantment

constants:

   include blakston.khd

resources:

   include haste.lkod

   Haste_name_rsc = "haste"
   Haste_icon_rsc = ihaste.bgf
   Haste_desc_rsc = \
      "Reduces the fatigue of running at full clip.  "
      "Requires five edible mushrooms to cast."
   
   Haste_on_rsc = \
      "Your blood begins to tingle, cleansing your leg muscles of fatigue "
      "with every heartbeat."
   Haste_off_rsc = \
      "Your feel the tide of energy in your blood recede, and running seems "
      "suddenly more taxing."
   Haste_already_enchanted_rsc = \
      "You're already resistant to running fatigue."
   Haste_success_rsc = "%s%s is charged with your magical energy."
   haste_enchantment_rsc = \
      "\n\nYour current %s enchantment reduces your exertion costs by %i%%."
   Haste_spell_intro = \
      "Kraanan Lv. 2: Allows you to use less stamina as you run."

   Haste_sound = khaste.ogg

classvars:

   vrName = Haste_name_rsc
   vrIcon = Haste_icon_rsc
   vrDesc = Haste_desc_rsc

   vrAlreadyEnchanted = Haste_already_enchanted_rsc
   vrEnchantment_On = Haste_On_rsc
   vrEnchantment_Off = Haste_Off_rsc
   vrSuccess = Haste_Success_rsc

   viSpell_num = SID_HASTE
   viSchool = SS_KRAANAN
   viSpell_level = 2
   viMana = 10
   viSpellExertion = 0


   viCast_time = 2000
   viMeditate_ratio = 10

   vrSpell_intro = Haste_spell_intro
   vrSucceed_wav = Haste_sound

properties:

   // The effectiveness of purge on the target spell as a percentage.
   // 100 will cause purge to remove its entire spellpower, 0 will
   // prevent purge from removing this spell.
   viPurgeFactor = 90

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Snack,5],plReagents);

      return;
   }

   GetStateValue(iSpellPower=0)
   {
      return iSpellPower;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG2_HASTED,#flagset=2,#value=True);

      return;
   }

   GetDuration(iSpellpower=0)
   {
      local iDuration;

      iDuration = 5 + ((15 * iSpellPower) / 100);   // 5 - 20 minutes,
      iDuration = iDuration * 1000 * 60;   // Convert to milliseconds

      return iDuration;
   }

   GetPotionClass()
   {
      return &HastePotion;
   }

   EffectDesc(who=$)
   {
      AddPacket(4,haste_enchantment_rsc, 4,Send(self,@GetName),
                4,Send(who,@GetEnchantedState,#what=self));
      
      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
