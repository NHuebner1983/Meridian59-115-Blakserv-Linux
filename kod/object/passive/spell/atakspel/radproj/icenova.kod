// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
IceNova is RadiusProjectile

constants:

   include blakston.khd

resources:

   include icenova.lkod

   icenova_name_rsc = "ice nova"
   icenova_icon_rsc = iicenova.bgf
   icenova_projectile_rsc = icebolt.bgf
   icenova_desc_rsc = \
      "Call upon the magic of Faren and propel forth a blast of ice, "
      "chilling and blistering the skin of those around you.  "
      "Requires one red and two blue mushrooms to cast."

   icenova_first_rsc = \
      "You channel the power of Faren and unleash a flurry of ice shards!"
   icenova_third_rsc = \
      "%s%s channels the power of Faren and unleashes a flurry of ice shards!"

   // These aren't in use at the moment.
   icenova_killed_someone = \
      "The ice shards fatally wound %s%s."
   icenova_cast_rsc = \
      "You let out a cry of pain as ice shards pierce your skin, "
      "causing ~r~B%i~n damage."
   icenova_hit_rsc = \
      "%s%s lets out a cry of pain as ice shards pierce %s skin "
      "for ~k~B%i~n damage."

   icenova_sound = fshatter.ogg

classvars:

   vrName = icenova_name_rsc
   vrDesc = icenova_desc_rsc
   vrIcon = icenova_icon_rsc

   viSpell_num = SID_ICE_NOVA
   viSchool = SS_FAREN
   viSpell_level = 5
   viMana = 15

   viChance_to_increase = 15
   viMeditate_ratio = 75
   vrSucceed_wav = icenova_sound

   viAttack_spell = ATCK_SPELL_ALL+ATCK_SPELL_COLD
   viOutlaw = TRUE
   viHarmful = TRUE
   viNoNewbieOffense = TRUE

   // Projectile lighting information.
   viProjectileLightFlags = LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC
   // Medium light range, out of 255.
   viProjectileLightIntensity = 125
   // Color is lightning.
   viProjectileLightColor = LIGHT_DBLUE

   vrProjectile_icon = icenova_projectile_rsc
   vbIsAreaEffect = TRUE

properties:

   piSpeed = 9

   piDamageMin = 11
   piDamageMax = 18

   piNumber = 12

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RedMushroom,1],plReagents);
      plReagents = Cons([&BlueMushroom,2],plReagents);

      return;
   }

   SendProjectileAnimation()
   {
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 2, 2, 2);  // 40ms between animations

      return;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=0,bItemCast=FALSE)
   {
      // This is an area damage spell, so add room messages
      // showing the spell being cast.
      Send(self,@DoIceNova,#who=who,#bItemCast=bItemCast);

      propagate;
   }

   DoIceNova(who=$,bItemCast=FALSE)
   {
      local i, oRoom, each_obj;

      if (who = $)
      {
         return;
      }

      oRoom = Send(who,@GetOwner);

      // Send a message to all in room of spellcasters actions
      foreach i in Send(oRoom,@GetHolderActive)
      {
         each_obj = First(i);

         if IsClass(each_obj,&User)
         {
            if each_obj = who
            {
               Send(each_obj,@MsgSendUser,#message_rsc=icenova_first_rsc);
            }
            else
            {
               Send(each_obj,@MsgSendUser,#message_rsc=icenova_third_rsc,
                     #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName));
            }
         }
      }

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
