// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
BowmakerEvilQuest is BowmakerQuestTemplate
// Quest template QST_ID_BOW_MAKER_EVIL = 21.
// Quest nodes 38, 41.
// QNT_ID_BOWMAKER_KICKOFF, QNT_ID_BOWMAKER_EVIL

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include bowmakerevilqt.lkod

   // Quest description.
   bowmakerevilquest_icon_rsc = avfeathr.bgf
   bowmakerevilquest_desc_rsc = \
      "Hanla zax'Ta has asked you to obtain the tailfeather of an "
      "avar shaman for her. Seek out and kill avar shamans until you "
      "find an undamaged tailfeather on the corpse.  Return to her "
      "when you are successful in finding one."

   bowmakerevilquest_kill_rsc = \
      "Hanla zax'Ta has asked you to obtain the tailfeather of an "
      "avar shaman for her. Seek out and kill avar shamans until you"
      "find an undamaged tailfeather on the corpse."
   bowmakerevilquest_return_rsc = \
      "Return to Hanla zax'Ta with the avar shaman tailfeather."

   // Quest resources.
   bowmaker_assign_4 = \
      "I use special materials to make my best arrows.  The tailfeathers "
      "of an avar magician are reputed to offer magical properties to any "
      "arrow they fletch.  Go, kill avar shamans until you find one which "
      "offers a suitably undamaged tailfeather."
   bowmaker_failure_4 = \
      "Subject: Avar feather\n"
      "Clearly, you were unable get an avar feather for me.  I understand, "
      "avar shamans are formidable opponents.  If you wish, return later, "
      "perhaps I will need more."

classvars:

   vrIcon = bowmakerevilquest_icon_rsc
   vrDesc = bowmakerevilquest_desc_rsc

   vrKill = bowmakerevilquest_kill_rsc
   vrReturn = bowmakerevilquest_return_rsc

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_BOW_MAKER_EVIL

   viIcon_Animation_start = 1

properties:

   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_KARMA_EVIL

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_BOWMAKER_KICKOFF, QNT_ID_BOWMAKER_EVIL];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #41 is bow maker quest (evil)
      // QNT_ID_BOWMAKER_EVIL
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MONSTER_ITEMCLASS,
               #monsterlist=[ &AvarShaman ],
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &AvarShamanFeather, 1 ] ],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &SwordShardD, 1 ] ],
               #quest_node_index=QNT_ID_BOWMAKER_EVIL)
      {
         Send(oQE,@SetQuestNodeNPCModifier,#index=QNT_ID_BOWMAKER_EVIL,
               #new_mod=QN_NPCMOD_SAME);
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_BOWMAKER_EVIL,
               #new_hint=bowmaker_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_BOWMAKER_EVIL,
               #new_hint=bowmaker_success_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_BOWMAKER_EVIL,
               #new_hint=bowmaker_failure_4);
         Send(oQE,@SetQuestNodeTimeLimit,#index=QNT_ID_BOWMAKER_EVIL,
               #new_limit=10800);
      }
      else
      {
         Debug("Error building QN: QNT_ID_BOWMAKER_EVIL");
      }

      propagate;
   }

   MainDesc(who=$,index=$,oQuestNode=$)
   {
      local oMob;

      oMob = Send(oQuestNode,@GetQuestMonster);
      if oMob = $
      {
         AddPacket(4,vrReturn);
      }
      else
      {
         AddPacket(4,vrKill);
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
