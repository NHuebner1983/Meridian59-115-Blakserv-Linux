// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
SporePupilQuest is QuestTemplate
// Quest nodes 95, 96, 97.
// QNT_ID_SPORE_PUPIL_ONE, QNT_ID_SPORE_PUPIL_TWO, QNT_ID_SPORE_PUPIL_THREE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include sporepupilqt.lkod

   // Quest description.
   sporepupilquest_icon_rsc = isporecl.bgf
   sporepupilquest_name_rsc = "sporeburst pupil quest"
   sporepupilquest_desc_rsc = \
      "Maleval has offered to teach you a powerful new spell, but has "
      "requested that you bring him rare magical items before he will do so."

   sporepupilquest_nodetwo_rsc = \
      "Maleval has offered to teach you a powerful new spell, but has "
      "requested that you obtain %s%s for him first."

   sporepupilquest_nodethree_rsc = \
      "Maleval has asked that you obtain another item for him, %s%s.  Bring "
      "it to him so that he will teach you the spell."

   sporepupilquest_completed = \
      "You have completed the sporeburst pupil quest.  Maleval will now "
      "teach you the sporeburst spell, provided you know at least level "
      "5 Qor and level 6 Faren.  He will also teach you the curse weapon "
      "spell, if you know level 4 Qor and level 5 Kraanan."

   // Quest resources.
   sporeburstlearn_assign_2 = \
      "Oh, %LORDLADY %NAME, it is ~Iindeed~I a pleasure to see you.  I have "
      "watched your progress in the arcane studies with great interest;  "
      "rarely have I encountered one so worthy of my tutelage.  You may know "
      "of my research in the magical arts.  Well, I have at last crafted an "
      "entirely new spell, grafted together from the energy fields of what "
      "the religious rabble would call Faren and Qor.  I will teach you my "
      "new incantation, if you but perform one small task for me:  bring "
      "me %INDEF_CARGO%CARGO, that I may research its mystical properties."
   sporeburstlearn_assign_2_h = \
      "Oh, %LORDLADY %NAME, it is ~Iindeed~I a pleasure to see you.  I have "
      "watched your progress in the arcane studies with great interest;  "
      "rarely have I encountered one so worthy of my tutelage.  You may know "
      "of my research in the magical arts.  Well, I have at last crafted "
      "an entirely new spell.  I will teach you my new incantation, if you "
      "but perform one small task for me:  bring me %INDEF_CARGO%CARGO, "
      "that I may research its mystical properties."
   sporeburstlearn_failure_2 = \
      "Subject:  Weak fool!\n"
      "I misjudged you, %NAME.  I believed you were capable of great things, "
      "heights of power unachieved even by the Necromancers of antiquity.  "
      "I see now that I was wrong."
   sporeburstlearn_success_2 = \
      "You are a worthy pupil, indeed, %NAME.  Ah, %INDEF_CARGO%CARGO!  Can "
      "you ~Ifeel~I its magical essence, coming off it in waves?  The power "
      "contained herein is far beyond the trivial religious significance "
      "those parlor priests attach to it.  This is the stuff of which the "
      "universe is fashioned! ..."
   sporeburstlearn_assign_3 = \
      "Ah yes, the spell, the spell.  Sporeburst!  Now bring me "
      "%INDEF_CARGO%CARGO, and I shall teach you that, and more - I have "
      "recently discovered another intriguing spell as well."
   sporeburstlearn_assign_3_h = \
      "Ah yes, the spell, the spell.  Curse weapon!  Now bring me "
      "%INDEF_CARGO%CARGO, and I shall teach you."
   sporeburstlearn_success_3 = \
      "It has been many moons since %INDEF_CARGO%CARGO last touched my "
      "lips.  Would you care for a sip?  It's ~Iquite~I refreshing.  Well, "
      "return when you wish to learn the sporeburst spell, and we'll have a "
      "little toast - ~ITo the foolish masses, who offer up their feeble "
      "bodies of blood and bone to be swallowed by silent stones or "
      "jigging delusory puppet-gods.~I"
   sporeburstlearn_success_3_h = \
      "It has been many moons since %INDEF_CARGO%CARGO last touched my "
      "lips.  Would you care for a sip?  It's ~Iquite~I refreshing.  Well, "
      "return when you wish to learn the curse weapon spell, and we'll have "
      "a little toast - ~ITo the foolish masses, who offer up their feeble "
      "bodies of blood and bone to be swallowed by silent stones or "
      "jigging delusory puppet-gods.~I"

classvars:

   vrIcon = sporepupilquest_icon_rsc
   vrName = sporepupilquest_name_rsc
   vrDesc = sporepupilquest_desc_rsc

   vrNodeTwo = sporepupilquest_nodetwo_rsc
   vrNodeThree = sporepupilquest_nodethree_rsc
   vrCompleted = sporepupilquest_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_SPOREBURST_LEARN

properties:

   piNumPlayers = 1
   piPlayerRestrict = Q_PLAYER_NOTSUCCEEDED | Q_PLAYER_NOTFAILED_RECENTLY
   plSpecialRestrict = $
   piMaxPlayers = 10
   piSchedulePct = 4

   piTimeLimit = 2 * 24 * 3600 // 2 days

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_SPORE_PUPIL_ONE, QNT_ID_SPORE_PUPIL_TWO,
                      QNT_ID_SPORE_PUPIL_THREE ];

      plSpecialRestrict = [ [ Q_R2_HAS_SPELL, SID_HOLD, 75 ] ];

      // Quest nodes with matching descriptions for the player.
      plQuestNodeDesc = [ [QNT_ID_SPORE_PUPIL_TWO, vrNodeTwo],
                          [QNT_ID_SPORE_PUPIL_THREE, vrNodeThree] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local lNPCs, oQE, oLib, oNPC;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #95 is sporeburst learn quest
      // QNT_ID_SPORE_PUPIL_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #quest_node_index=QNT_ID_SPORE_PUPIL_ONE)
      {
         lNPCs = $;
         foreach oNPC in Send(oLib,@GetOccupationList,#cNPC_class=&DarkWizard)
         {
            lNPCs = Cons(oNPC, lNPCs);
         }
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_SPORE_PUPIL_ONE,
               #new_NPC_list=lNPCs);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SPORE_PUPIL_ONE");
      }

      // Template #96 is sporeburst learn quest
      // QNT_ID_SPORE_PUPIL_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEMFINDCLASS,
               #NPC_modifier=QN_NPCMOD_SAME,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingShalille, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &OfferingFaren, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &OfferingKraanan, 1 ],
                            [ QN_PRIZETYPE_ITEMCLASS, &OfferingRiija, 1 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SPORE_PUPIL_TWO)
      {
         // Happy land subs.
         if NOT Send(SYS,@IsPKAllowed)
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SPORE_PUPIL_TWO,
                  #new_hint=sporeburstlearn_assign_2_h);
         }
         else
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SPORE_PUPIL_TWO,
                  #new_hint=sporeburstlearn_assign_2);
         }
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_SPORE_PUPIL_TWO,
               #new_hint=sporeburstlearn_success_2);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_SPORE_PUPIL_TWO,
               #new_hint=sporeburstlearn_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SPORE_PUPIL_TWO");
      }

      // Template #97 is sporeburst learn quest
      // QNT_ID_SPORE_PUPIL_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_ITEMFINDCLASS,
               #NPC_modifier=QN_NPCMOD_SAME,
               #cargolist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingQor, 1 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SPORE_PUPIL_THREE)
      {
         // Happy land subs.
         if NOT Send(SYS,@IsPKAllowed)
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SPORE_PUPIL_THREE,
                  #new_hint=sporeburstlearn_assign_3_h);
            Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_SPORE_PUPIL_THREE,
                  #new_hint=sporeburstlearn_success_3_h);
         }
         else
         {
            Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SPORE_PUPIL_THREE,
                  #new_hint=sporeburstlearn_assign_3);
            Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_SPORE_PUPIL_THREE,
                  #new_hint=sporeburstlearn_success_3);
         }
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_SPORE_PUPIL_THREE,
               #new_hint=sporeburstlearn_failure_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SPORE_PUPIL_THREE");
      }

      return;
   }

   ExtraDesc(index=$,oQuestNode=$)
   {
      local oCargo;

      if oQuestNode = $
         OR index = $
      {
         return;
      }

      oCargo = Send(oQuestNode,@GetCargo);

      if index = QNT_ID_SPORE_PUPIL_TWO
         OR QNT_ID_SPORE_PUPIL_THREE
      {
         AddPacket(4,Send(oCargo,@GetIndef), 4,Send(oCargo,@GetName));

         return;
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
