// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
ShalOfferingQuest is OfferingQuestTemplate
// Quest template QST_ID_SHALILLE_OFFERING = 27
// Quest nodes 68, 69, 70, 71, 72.
// QNT_ID_SHAL_OFFERING_ONE, QNT_ID_SHAL_OFFERING_TWO, QNT_ID_SHAL_OFFERING_THREE
// QNT_ID_SHAL_OFFERING_FOUR, QNT_ID_SHAL_OFFERING_FIVE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include shalofferingqt.lkod

   // Quest description.
   shalofferingqt_icon_rsc = tear.bgf
   shalofferingqt_name_rsc = "Shal'ille offering quest"
   shalofferingqt_desc_rsc = \
      "Priestess Xiana has invited you to chant with her, to give strength "
      "to your Mistress."

   shalofferingqt_completed = \
      "You have given part of your life essence in order to strengthen "
      "your Mistress.  In return, you have received a powerful magical "
      "item.  When placed at one of the Shrines located around the "
      "world, the Shrine will be dedicated to Shal'ille and her spells "
      "will have more power."

   // Quest resources.
   shalilleoffering_assign_2 = \
      "`b%NAME, pure disciple of our Lady of the Silver Light, will you "
      "give of yourself so that your Goddess may be stronger?"
   shalilleoffering_assign_3 = \
      "`bWorship with me, then.  Together, we will draw upon our own life "
      "essence to birth a symbol of our Lady's purity.  Rarely are the "
      "stars so well aligned for the holy ceremony we shall undertake, so "
      "we must speak quickly.  Incant these words with me: %CARGO"
   shalilleoffering_trigger_3 = \
      "By the light of a bright sun, we beseech thee, Shal'ille, rebirth us"
   shalilleoffering_failure_3 = "Subject: Alas\n"
      "This day we have not reached high enough, yet we have reached, and "
      "that is enough.  Today we are left weak and empty-handed, but when "
      "the time is right Shal'ille shall lift us, my disciple."
   shalilleoffering_assign_4 = "`b%CARGO!"
   shalilleoffering_success_5 = \
      "`bPraise Shal'ille!  Go now, carry forth into the world this "
      "symbol of our Lady's purity of spirit!"

classvars:

   vrIcon = shalofferingqt_icon_rsc
   vrName = shalofferingqt_name_rsc
   vrDesc = shalofferingqt_desc_rsc

   vrCompleted = shalofferingqt_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_SHALILLE_OFFERING

   viIcon_Animation_start = 1

properties:

   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_KARMA_GOOD \
                      | Q_PLAYER_NOTNEWBIE

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_SHAL_OFFERING_ONE, QNT_ID_SHAL_OFFERING_TWO,
                      QNT_ID_SHAL_OFFERING_THREE, QNT_ID_SHAL_OFFERING_FOUR,
                      QNT_ID_SHAL_OFFERING_FOUR, QNT_ID_SHAL_OFFERING_FOUR,
                      QNT_ID_SHAL_OFFERING_FIVE];

      // Quest nodes with matching descriptions for the player.
      // No description needed for quest node QNT_ID_SHAL_DISCIPLE_ONE.
      plQuestNodeDesc = [ [QNT_ID_SHAL_OFFERING_TWO, vrAccept],
                          [QNT_ID_SHAL_OFFERING_THREE, vrChant],
                          [QNT_ID_SHAL_OFFERING_FOUR, vrChant],
                          [QNT_ID_SHAL_OFFERING_FIVE, vrChant] ];

      plSpecialRestrict = [ [ Q_R2_HAS_SPELL_SCHOOL, SS_SHALILLE, 15 ] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #68 is shal'ille offering quest kickoff
      // QNT_ID_SHAL_OFFERING_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #quest_node_index=QNT_ID_SHAL_OFFERING_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_SHAL_OFFERING_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&ShalillePriestess));
      }
      else
      {
         Debug("Error building QN: QNT_ID_SHAL_OFFERING_ONE");
      }

      // Template #69 is shal'ille offering quest, say "yes"
      // QNT_ID_SHAL_OFFERING_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[offering_trigger_2],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SHAL_OFFERING_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SHAL_OFFERING_TWO,
               #new_hint=shalilleoffering_assign_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SHAL_OFFERING_TWO");
      }

      // Template #70 is shal'ille offering quest, incant
      // QNT_ID_SHAL_OFFERING_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[shalilleoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SHAL_OFFERING_THREE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SHAL_OFFERING_THREE,
               #new_hint=shalilleoffering_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_SHAL_OFFERING_THREE,
               #new_hint=shalilleoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SHAL_OFFERING_THREE");
      }

      // Template #71 is shal'ille offering quest, incant
      // QNT_ID_SHAL_OFFERING_FOUR
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[shalilleoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SHAL_OFFERING_FOUR)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SHAL_OFFERING_FOUR,
               #new_hint=shalilleoffering_assign_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_SHAL_OFFERING_FOUR,
               #new_hint=shalilleoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SHAL_OFFERING_FOUR");
      }

      // Template #72 is shal'ille offering quest, incant
      // QNT_ID_SHAL_OFFERING_FIVE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[shalilleoffering_trigger_3],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingShalille, 1 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -4, 4 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -1, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_MYSTICISM , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_SHAL_OFFERING_FIVE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_SHAL_OFFERING_FIVE,
               #new_hint=shalilleoffering_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_SHAL_OFFERING_FIVE,
               #new_hint=shalilleoffering_success_5);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_SHAL_OFFERING_FIVE,
               #new_hint=shalilleoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_SHAL_OFFERING_FIVE");
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
