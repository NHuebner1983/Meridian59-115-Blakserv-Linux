// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


/////////////////////////////////////////////////////////////////////////////////
RiijaOfferingQuest is OfferingQuestTemplate
// Quest template QST_ID_RIIJA_OFFERING = 30
// Quest nodes 83, 84, 85, 86, 87.
// QNT_ID_RIIJA_OFFERING_ONE, QNT_ID_RIIJA_OFFERING_TWO, QNT_ID_RIIJA_OFFERING_THREE
// QNT_ID_RIIJA_OFFERING_FOUR, QNT_ID_RIIJA_OFFERING_FIVE

// QuestTemplate is
// [ quest template index
//   quest template object
//   number of players,
//   type of quest,
//   eligible player restrictions,
//   quest nodes,
//   max number active,
//   currently active quests,
//   percent chance of scheduling (on timer firing)
//   special restrictions ]

constants:

   include blakston.khd

resources:

   include riijaofferingqt.lkod

   // Quest description.
   riijaofferingqt_icon_rsc = lies.bgf
   riijaofferingqt_name_rsc = "Riija offering quest"
   riijaofferingqt_desc_rsc = \
      "The Riija Monk has invited you to chant with him, to give strength "
      "to Lord Riija."

   riijaofferingqt_completed = \
      "Riija has bestowed temporary power upon you, in return for some "
      "of your life essence.  You have also received a powerful magical "
      "item.  When placed at one of the Shrines located around the "
      "world, the Shrine will be dedicated to Riija and his spells "
      "will have more power."

   // Quest resources.
   riijaoffering_assign_2 = \
      "%NAME, Lord Fool wishes to fortify you.  Will you chant with me?"
   riijaoffering_assign_3 = \
      "Incant these words with me: %CARGO.  Feel your power grow with "
      "every word spoken."
   riijaoffering_trigger_3 = "Your words speak wisdom through me, Lord Riija"
   riijaoffering_failure_3 = \
      "Subject: Fool!\n"
      "He who stands at the edge of a cliff yet walks blithely on is both "
      "God and Fool.  Walk on, servant of seeming."
   riijaoffering_assign_4 = "%CARGO!"
   riijaoffering_success_5 = \
      "Behold, the double-edged blessing of Riija.  Bear it forth into "
      "the world and reap the chaos it sows!  Remember these ancient "
      "words of wisdom:  this is really going to hurt in the morning."

classvars:

   vrIcon = riijaofferingqt_icon_rsc
   vrName = riijaofferingqt_name_rsc
   vrDesc = riijaofferingqt_desc_rsc

   vrCompleted = riijaofferingqt_completed

   // The QST_ID constant for this quest.
   viQuestID = QST_ID_RIIJA_OFFERING

   viIcon_Animation_start = 1

properties:

   piPlayerRestrict = Q_PLAYER_NOTTRIED_RECENTLY | Q_PLAYER_NOTNEWBIE

messages:

   Constructor()
   {
      // Quest node list to send to QuestEngine.
      plQuestNodes = [QNT_ID_RIIJA_OFFERING_ONE, QNT_ID_RIIJA_OFFERING_TWO,
                      QNT_ID_RIIJA_OFFERING_THREE, QNT_ID_RIIJA_OFFERING_FOUR,
                      QNT_ID_RIIJA_OFFERING_FOUR, QNT_ID_RIIJA_OFFERING_FOUR,
                      QNT_ID_RIIJA_OFFERING_FIVE];

      // Quest nodes with matching descriptions for the player.
      // No description needed for quest node QNT_ID_SHAL_DISCIPLE_ONE.
      plQuestNodeDesc = [ [QNT_ID_RIIJA_OFFERING_TWO, vrAccept],
                          [QNT_ID_RIIJA_OFFERING_THREE, vrChant],
                          [QNT_ID_RIIJA_OFFERING_FOUR, vrChant],
                          [QNT_ID_RIIJA_OFFERING_FIVE, vrChant] ];

      plSpecialRestrict = [ [ Q_R2_HAS_SPELL_SCHOOL, SS_RIIJA, 15 ] ];

      propagate;
   }

   SendQuestNodeTemplates()
   {
      local oQE, oLib;

      oQE = Send(SYS,@GetQuestEngine);
      oLib = Send(SYS,@GetLibrary);

      // Template #83 is riija offering quest kickoff
      // QNT_ID_RIIJA_OFFERING_ONE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_SHOWUP,
               #quest_node_index=QNT_ID_RIIJA_OFFERING_ONE)
      {
         Send(oQE,@SetQuestNodeNPCList,#index=QNT_ID_RIIJA_OFFERING_ONE,
               #new_NPC_list=Send(oLib,@GetOccupationList,
                                 #cNPC_class=&RiijaMonk));
      }
      else
      {
         Debug("Error building QN: QNT_ID_RIIJA_OFFERING_ONE");
      }

      // Template #84 is riija offering quest, say "yes"
      // QNT_ID_RIIJA_OFFERING_TWO
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[offering_trigger_2],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_RIIJA_OFFERING_TWO)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_RIIJA_OFFERING_TWO,
               #new_hint=riijaoffering_assign_2);
      }
      else
      {
         Debug("Error building QN: QNT_ID_RIIJA_OFFERING_TWO");
      }

      // Template #85 is riija offering quest, incant
      // QNT_ID_RIIJA_OFFERING_THREE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[riijaoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , 3, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -2, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_RIIJA_OFFERING_THREE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_RIIJA_OFFERING_THREE,
               #new_hint=riijaoffering_assign_3);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_RIIJA_OFFERING_THREE,
               #new_hint=riijaoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_RIIJA_OFFERING_THREE");
      }

      // Template #86 is riija offering quest, incant
      // QNT_ID_RIIJA_OFFERING_FOUR
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[riijaoffering_trigger_3],
               #prizelist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , 3, 4 ],
                            [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -2, 7 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -5, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_RIIJA_OFFERING_FOUR)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_RIIJA_OFFERING_FOUR,
               #new_hint=riijaoffering_assign_4);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_RIIJA_OFFERING_FOUR,
               #new_hint=riijaoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_RIIJA_OFFERING_FOUR");
      }

      // Template #87 is riija offering quest, incant
      // QNT_ID_RIIJA_OFFERING_FIVE
      if Send(oQE,@AddQuestNodeTemplate,#questnode_type=QN_TYPE_MESSAGE,
               #NPC_modifier=QN_NPCMOD_SAME,#cargolist=[riijaoffering_trigger_3],
               #prizelist=[ [ QN_PRIZETYPE_ITEMCLASS, &OfferingRiija, 1 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , 25, 1 ],
                             [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -24, 4 ] ],
               #penaltylist=[ [QN_PRIZETYPE_BOON, QN_PRIZE_BOON_INTELLECT , -6, 2 ] ],
               #timelimit=piTimeLimit,#quest_node_index=QNT_ID_RIIJA_OFFERING_FIVE)
      {
         Send(oQE,@SetQuestNodeAssignHint,#index=QNT_ID_RIIJA_OFFERING_FIVE,
               #new_hint=riijaoffering_assign_4);
         Send(oQE,@SetQuestNodeSuccessHint,#index=QNT_ID_RIIJA_OFFERING_FIVE,
               #new_hint=riijaoffering_success_5);
         Send(oQE,@SetQuestNodeFailureHint,#index=QNT_ID_RIIJA_OFFERING_FIVE,
               #new_hint=riijaoffering_failure_3);
      }
      else
      {
         Debug("Error building QN: QNT_ID_RIIJA_OFFERING_FIVE");
      }

      return;
   }

end
/////////////////////////////////////////////////////////////////////////////////
