// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
Sign is PassiveObject

constants:

   include blakston.khd

resources:

   include sign.lkod

   sign_name_rsc = "sign"
   sign_icon_rsc = sign.bgf
   sign_icon_newbie_rsc = razsign1.bgf
   sign_desc_rsc = "This is an informational sign."
   sign_insc_rsc = " "

classvars:

   viObject_flags = OF_SIGN | OF_DISPLAY_NAME

properties:

   vrName = sign_name_rsc
   vrIcon = sign_icon_rsc
   vrDesc = sign_desc_rsc
   vrInsc = sign_insc_rsc

   piNameColor = NC_DM

messages:

   Constructor(name = $,icon = $,desc = $,insc = $,newbie = FALSE)
   {
      if name <> $
      {
         vrName = name;
      }

      if icon <> $
      {
         vrIcon = icon;
      }

      if newbie = TRUE
      {
         vrIcon = sign_icon_newbie_rsc;
      }

      if desc <> $
      {
         vrDesc = desc;
      }

      if insc <> $
      {
         vrInsc = insc;
      }

      propagate;
   }

   HasInscription()
   {
      return (vrInsc <> sign_insc_rsc);
   }

   GetInscription()
   {
      return vrInsc;
   }

   ShowInscription()
   {
      AddPacket(4, vrInsc);

      return;
   }

   ReqNewOwner(what = $)
   {
      return FALSE;
   }

   SendLightingInformation()
   {
      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC | LIGHT_FLAG_HIGHLIGHT));
      AddPacket(1,255);
      AddPacket(2,LIGHT_BWHITE);

      return;
   }

end
////////////////////////////////////////////////////////////////////////////////
